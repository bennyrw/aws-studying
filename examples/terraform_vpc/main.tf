provider "aws" {
  region  = "${var.aws_region}"
  profile = "${var.aws_profile}"
}

# -- IAM --

resource "aws_iam_instance_profile" "s3_access_profile" {
  name = "s3_access"
  role = "${aws_iam_role.s3_access_role.name}"
}

resource "aws_iam_role_policy" "s3_access_policy" {
  name = "s3_access_policy"
  role = "${aws_iam_role.s3_access_role.id}"

  policy = <<EOF
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Effect": "Allow",
      "Action": "s3:*",
      "Resource": "*"
    }
  ]
}
EOF
}

resource "aws_iam_role" "s3_access_role" {
  name = "s3_access_role"

  assume_role_policy = <<EOF
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Action": "sts:AssumeRole",
      "Principal": {
        "Service": "ec2.amazonaws.com"
      },
      "Effect": "Allow"
    }
  ]
}
EOF
}

# -- VPC --

resource "aws_vpc" "wp_vpc" {
  cidr_block           = "${var.vpc_cidr}"
  enable_dns_hostnames = true
  enable_dns_support   = true

  tags = {
    Name = "wp_vpc"
  }
}

resource "aws_internet_gateway" "wp_internet_gateway" {
  vpc_id = "${aws_vpc.wp_vpc.id}"

  tags {
    Name = "wp_igw"
  }
}

resource "aws_route_table" "wp_public_rt" {
  vpc_id = "${aws_vpc.wp_vpc.id}"

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = "${aws_internet_gateway.wp_internet_gateway.id}"
  }

  tags {
    Name = "wp_public"
  }
}

resource "aws_default_route_table" "wp_private_rt" {
  default_route_table_id = "${aws_vpc.wp_vpc.default_route_table_id}"

  tags {
    Name = "wp_private"
  }
}

# Subnets

resource "aws_subnet" "wp_public1_subnet" {
  vpc_id                  = "${aws_vpc.wp_vpc.id}"
  cidr_block              = "${var.cidrs["public1"]}"
  map_public_ip_on_launch = true
  availability_zone       = "${data.aws_availability_zones.available.names[0]}"

  tags {
    Name = "wp_public1"
  }
}

resource "aws_subnet" "wp_public2_subnet" {
  vpc_id                  = "${aws_vpc.wp_vpc.id}"
  cidr_block              = "${var.cidrs["public2"]}"
  map_public_ip_on_launch = true
  availability_zone       = "${data.aws_availability_zones.available.names[1]}"

  tags {
    Name = "wp_public2"
  }
}

resource "aws_subnet" "wp_private1_subnet" {
  vpc_id                  = "${aws_vpc.wp_vpc.id}"
  cidr_block              = "${var.cidrs["private1"]}"
  map_public_ip_on_launch = false
  availability_zone       = "${data.aws_availability_zones.available.names[0]}"

  tags {
    Name = "wp_private1"
  }
}

resource "aws_subnet" "wp_private2_subnet" {
  vpc_id                  = "${aws_vpc.wp_vpc.id}"
  cidr_block              = "${var.cidrs["private2"]}"
  map_public_ip_on_launch = false
  availability_zone       = "${data.aws_availability_zones.available.names[1]}"

  tags {
    Name = "wp_private2"
  }
}

resource "aws_subnet" "wp_rds1_subnet" {
  vpc_id                  = "${aws_vpc.wp_vpc.id}"
  cidr_block              = "${var.cidrs["rds1"]}"
  map_public_ip_on_launch = false
  availability_zone       = "${data.aws_availability_zones.available.names[0]}"

  tags {
    Name = "wp_rds1"
  }
}

resource "aws_subnet" "wp_rds2_subnet" {
  vpc_id                  = "${aws_vpc.wp_vpc.id}"
  cidr_block              = "${var.cidrs["rds2"]}"
  map_public_ip_on_launch = false
  availability_zone       = "${data.aws_availability_zones.available.names[1]}"

  tags {
    Name = "wp_rds2"
  }
}

resource "aws_subnet" "wp_rds3_subnet" {
  vpc_id                  = "${aws_vpc.wp_vpc.id}"
  cidr_block              = "${var.cidrs["rds3"]}"
  map_public_ip_on_launch = false
  availability_zone       = "${data.aws_availability_zones.available.names[2]}"

  tags {
    Name = "wp_rds3"
  }
}

# group together the DB subnets
resource "aws_db_subnet_group" "wp_rds_subnetgroup" {
  name = "wp_rds_subnetgroup"

  subnet_ids = [
    "${aws_subnet.wp_rds1_subnet.id}",
    "${aws_subnet.wp_rds2_subnet.id}",
    "${aws_subnet.wp_rds3_subnet.id}",
  ]

  tags {
    Name = "wp_rds_sng"
  }
}

# create public associations
resource "aws_route_table_association" "wp_public1_assoc" {
  subnet_id      = "${aws_subnet.wp_public1_subnet.id}"
  route_table_id = "${aws_route_table.wp_public_rt.id}"
}

resource "aws_route_table_association" "wp_public2_assoc" {
  subnet_id      = "${aws_subnet.wp_public2_subnet.id}"
  route_table_id = "${aws_route_table.wp_public_rt.id}"
}

# create private associations
resource "aws_route_table_association" "wp_private1_assoc" {
  subnet_id      = "${aws_subnet.wp_private1_subnet.id}"
  route_table_id = "${aws_default_route_table.wp_private_rt.id}"
}

resource "aws_route_table_association" "wp_private2_assoc" {
  subnet_id      = "${aws_subnet.wp_private2_subnet.id}"
  route_table_id = "${aws_default_route_table.wp_private_rt.id}"
}

# --- Security groups ---

resource "aws_security_group" "wp_dev_sg" {
  name        = "wp_dev_sg"
  description = "Used to access the dev instance, only from my ip"
  vpc_id      = "${aws_vpc.wp_vpc.id}"

  # ssh
  ingress {
    from_port   = 22
    to_port     = 22
    protocol    = "tcp"
    cidr_blocks = ["${var.localip}"]
  }

  # http
  ingress {
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = ["${var.localip}"]
  }

  # allow everything out
  egress {
    from_port   = 0             # everything
    to_port     = 0             # everything
    protocol    = "-1"          # -1 = everything
    cidr_blocks = ["0.0.0.0/0"]
  }
}

resource "aws_security_group" "wp_public_sg" {
  name        = "wp_public_sg"
  description = "Used to access the ELB for public access from any ip"
  vpc_id      = "${aws_vpc.wp_vpc.id}"

  # http
  ingress {
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  # allow everything out
  egress {
    from_port   = 0             # everything
    to_port     = 0             # everything
    protocol    = "-1"          # -1 = everything
    cidr_blocks = ["0.0.0.0/0"]
  }
}

resource "aws_security_group" "wp_private_sg" {
  name        = "wp_private_sg"
  description = "Used for private instances"
  vpc_id      = "${aws_vpc.wp_vpc.id}"

  # allow anything internally
  ingress {
    from_port   = 0                   # any
    to_port     = 0                   # any
    protocol    = "-1"                # any
    cidr_blocks = ["${var.vpc_cidr}"]
  }

  # allow everything out
  egress {
    from_port   = 0             # any
    to_port     = 0             # any
    protocol    = "-1"          # any
    cidr_blocks = ["0.0.0.0/0"]
  }
}

resource "aws_security_group" "wp_rds_sg" {
  name        = "wp_rds_sg"
  description = "Used for RDS instances"
  vpc_id      = "${aws_vpc.wp_vpc.id}"

  # SQL from public/private security groups
  ingress {
    from_port = 3306
    to_port   = 3306
    protocol  = "tcp"

    security_groups = [
      "${aws_security_group.wp_dev_sg.id}",
      "${aws_security_group.wp_public_sg.id}",
      "${aws_security_group.wp_private_sg.id}",
    ]
  }
}

# -- VPC endpoint for S3 and the bucket itself --

resource "aws_vpc_endpoint" "wp_private-s3_endpoint" {
  vpc_id = "${aws_vpc.wp_vpc.id}"

  # access S3 from a specific region
  service_name = "com.amazonaws.${var.aws_region}.s3"

  # attach to route table so goes over the endpoint rather than over the internet
  route_table_ids = [
    "${aws_vpc.wp_vpc.main_route_table_id}",
    "${aws_route_table.wp_public_rt.id}",
  ]

  policy = <<POLICY
{
  "Statement": [
    {
      "Action": "*",
      "Effect": "Allow",
      "Resource": "*",
      "Principal": "*"
    }
  ]
}
POLICY
}

# first generate a random ID as bucket has to be unique across entire AWS
# https://registry.terraform.io/providers/hashicorp/random/latest/docs/resources/id
# (when first adding this, need to run `terraform init` to download the plugin)
resource "random_id" "wp_code_bucket" {
  byte_length = 2
}

resource "aws_s3_bucket" "code" {
  bucket = "${var.domain_name}-${random_id.wp_code_bucket.dec}"

  # public shouldn't be public - our endpoints and policy above control access
  acl = "private"

  # override destroy protection (you might not want this!)
  force_destroy = true

  tags = {
    Name = "code bucket"
  }
}

# --- RDS ---

resource "aws_db_instance" "wp_db" {
  allocated_storage = 10       # GB
  engine            = "mysql"
  engine_version    = "5.6.27"

  # size of the server hosting the db
  instance_class = "${var.db_instance_class}"

  name     = "${var.dbname}"
  username = "${var.dbuser}"
  password = "${var.dbpassword}"

  db_subnet_group_name = "${aws_db_subnet_group.wp_rds_subnetgroup.name}"

  vpc_security_group_ids = [
    "${aws_security_group.wp_rds_sg.id}",
  ]

  # apparently without this, won't be able to destroy resources properly
  skip_final_snapshot = true
}

# --- Dev server ---

resource "aws_key_pair" "wp_auth" {
  key_name   = "${var.key_name}"
  public_key = "${file(var.public_key_path)}"
}

resource "aws_instance" "wp_dev" {
  instance_type = "${var.dev_instance_type}"
  ami           = "${var.dev_ami}"

  tags {
    Name = "wp_dev"
  }

  key_name = "${aws_key_pair.wp_auth.id}"

  vpc_security_group_ids = [
    "${aws_security_group.wp_dev_sg.id}",
  ]

  iam_instance_profile = "${aws_iam_instance_profile.s3_access_profile.id}"
  subnet_id            = "${aws_subnet.wp_public1_subnet.id}"

  # run a local command on your system (setup hosts file for use with ansible)
  provisioner "local-exec" {
    command = <<EOD
cat <<EOF > aws_hosts
[dev]
${aws_instance.wp_dev.public_ip}
[dev:vars]
s3code=${aws_s3_bucket.code.bucket}
domain=${var.domain_name}
EOF
EOD
  }

  # run another local command to wait for the instance to come up
  provisioner "local-exec" {
    command = "aws ec2 wait instance-status-ok --instance-ids ${aws_instance.wp_dev.id} --profile superhero"
  }

  # run ansible locally to install wordpress from the playbook
  provisioner "local-exec" {
    command = "ansible --playbook -i aws_hosts wordpress.yml"
  }
}

# -- Elastic Load Balancer (n.b. this is outdated now, should use Application/Network Load Balancer instead) --

resource "aws_elb" "wp_elb" {
  name = "${var.domain_name}-elb"

  # can serve traffic from these
  subnets = [
    "${aws_subnet.wp_public1_subnet.id}",
    "${aws_subnet.wp_public2_subnet.id}",
  ]

  security_groups = [
    # http from anywhere
    "${aws_security_group.wp_public_sg.id}",
  ]

  listener {
    instance_port     = 80
    instance_protocol = "http"
    lb_port           = 80
    lb_protocol       = "http"
  }

  health_check {
    healthy_threshold   = "${var.elb_healthy_threshold}"
    unhealthy_threshold = "${var.elb_unhealthy_threshold}"
    timeout             = "${var.elb_timeout}"
    target              = "TCP:80"
    interval            = "${var.elb_interval}"
  }

  # distribute all requests equally between all of instances (useful in case 1 AZ becomes larger than the other)
  cross_zone_load_balancing = true

  idle_timeout = 400

  # allow connections to finish before destorying
  connection_draining         = true
  connection_draining_timeout = 400

  tags {
    Name = "wp_${var.domain_name}-elb"
  }
}

# ------ 'Golden AMI' - created from the dev server and then used as AMI for the auto-scaling group ------

resource "random_id" "golden_ami" {
  byte_length = 2
}

resource "aws_ami_from_instance" "wp_golden" {
  name               = "wp_ami-${random_id.golden_ami.b64}" # base64
  source_instance_id = "${aws_instance.wp_dev.id}"

  # create userdata file that will be run on each instance in the auto-scaling group
  # this will cause each instance to pull code from the bucket every 5 minutes, keeping them up to date
  provisioner "local-exec" {
    command = <<EOD
cat <<EOF > userdata
#!/bin/bash
/usr/bin/aws s3 sync s3://${aws_s3_bucket.code.bucket} /var/www/html/
/bin/touch /var/spool/cron/root
sudo /bin/echo '*/5 * * * * aws s3 sync s3://${aws_s3_bucket.code.bucket} /var/www/html' >> /var/spool/cron/root
EOF
EOD
  }
}

# --- Auto-scaling group & launch configuration ---

resource "aws_launch_configuration" "wp_lc" {
  name_prefix   = "wp_lc-"
  image_id      = "${aws_ami_from_instance.wp_golden.id}"
  instance_type = "${var.lc_instance_type}"

  security_groups = [
    "${aws_security_group.wp_private_sg.id}",
  ]

  # allow access to s3
  iam_instance_profile = "${aws_iam_instance_profile.s3_access_profile.id}"

  # allow shell access
  key_name = "${aws_key_pair.wp_auth.id}"

  # additional startup commands, setup above
  user_data = "${file("userdata")}"

  lifecycle {
    # want to avoid downtime!
    create_before_destroy = true
  }
}

resource "aws_autoscaling_group" "wp_asg" {
  name                      = "asg-${aws_launch_configuration.wp_lc.id}"
  max_size                  = "${var.asg_max}"
  min_size                  = "${var.asg_min}"
  desired_capacity          = "${var.asg_capacity}"
  health_check_grace_period = "${var.asg_grace}"
  health_check_type         = "${var.asg_health_check_type}"

  # allow destroy without issues
  force_delete = true

  load_balancers = [
    "${aws_elb.wp_elb.id}",
  ]

  # deploy into these zones
  vpc_zone_identifier = [
    "${aws_subnet.wp_private1_subnet.id}",
    "${aws_subnet.wp_private2_subnet.id}",
  ]

  launch_configuration = "${aws_launch_configuration.wp_lc.name}"

  tag {
    key                 = "Name"
    value               = "wp_asg-instance"
    propagate_at_launch = true
  }

  lifecycle {
    create_before_destroy = true
  }
}

# --- Route 53 ---

# Primary (Public) Zone

resource "aws_route53_zone" "primary" {
  name = "${var.domain_name}.com"

  # setup any domains you want but keep same nameservers
  delegation_set_id = "${var.r53_delegation_set}"
}

# WWW
resource "aws_route53_record" "www" {
  zone_id = "${aws_route53_zone.primary.zone_id}"
  name    = "www.${var.domain_name}.com"
  type    = "A"

  # point to ELB
  alias {
    name                   = "${aws_elb.wp_elb.dns_name}"
    zone_id                = "${aws_elb.wp_elb.zone_id}"
    evaluate_target_health = false
  }
}

# DEV - allow access to the wp_dev instance
resource "aws_route53_record" "dev" {
  zone_id = "${aws_route53_zone.primary.zone_id}"
  name    = "dev.${var.domain_name}.com"
  type    = "A"

  # you can also manually set a TTL
  ttl = 300

  records = [
    "${aws_instance.wp_dev.public_ip}",
  ]
}

# Private Zone

resource "aws_route53_zone" "secondary" {
  name = "${var.domain_name}.com"

  vpc {
    vpc_id = "${aws_vpc.wp_vpc.id}"
  }
}

# DB
resource "aws_route53_record" "db" {
  zone_id = "${aws_route53_zone.secondary.zone_id}"
  name    = "db.${var.domain_name}.com"
  type    = "CNAME"
  ttl     = "300"

  records = [
    "${aws_db_instance.wp_db.address}",
  ]
}
